group 'com.hprog99'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'idea'

task annotationProcessing(type: JavaCompile, group: 'build') {
    source = sourceSets.main.java
    classpath = configurations.compile + configurations.compileOnly
    destinationDir = project.file('src/main/generated')
    options.compilerArgs = [
            "-proc:only",
            "-processor", "io.vertx.codegen.CodeGenProcessor",
            "-AoutputDirectory=${destinationDir.absolutePath}"
    ]
}

sourceSets {
    main {
        java {
            srcDirs += 'src/main/generated'
        }
    }
}

compileJava {
    targetCompatibility = 1.8
    sourceCompatibility = 1.8

    dependsOn annotationProcessing
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile "io.vertx:vertx-core:3.4.1"
    compile 'io.vertx:vertx-web:3.4.1'
    compile group: 'io.vertx', name: 'vertx-codegen', version: '3.4.1'
    testCompile 'io.vertx:vertx-unit:3.4.1'
    compile 'io.vertx:vertx-redis-client:3.4.1'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

jar {
    // by default fat jar
    archiveName = 'vertx-hprog99-todo-backend-fat.jar'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    manifest {
        attributes 'Main-Class': 'io.vertx.core.Launcher'
        attributes 'Main-Verticle': 'com.hprog99.verticles.SingleApplicationVerticle'
    }
}
